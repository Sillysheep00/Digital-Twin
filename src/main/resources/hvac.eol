'Starting realistic HVAC simulation...'.println();
var updatedSystems = 0;

// Simulation Parameters
var outdoorTemp = simulateData.outdoorTemperature;
var totalBuildingOccupancy = simulateData.occupancy;

var targetTemp = 22.0d; // target indoor temperature
var comfortZone = 1.0d; // 设定温度上下浮动的缓冲空间 (e.g., target is 22, so we cool at 23, heat at 21)
var uninitializedTemp = 0.0d; // The default value in the model

// How fast the HVAC can change the temp (higher is more powerful)
var HEAT_GAIN_PER_PERSON = 0.01d;

var AC_POWER_COEFFICIENT = 0.1d; // How fast AC cools
var HEATER_POWER_COEFFICIENT = 0.12d; // How fast heater heats

var AC_POWER_USAGE = 5.0d; // Power in KiloWatts for cooling
var HEATER_POWER_USAGE = 8.0d; // Power in KiloWatts for heating

//Coefficient of performance
var COOLING_COP = 3.5;
var HEATER_COP = 3.0;

// A small value to add for initial temperature
var INITIAL_INDOOR_OFFSET = 3.0d;

//----暂时用-----------------------------------------
if (simulateData.date == "2018-05-22 00:00:00") {
    "Resetting Energy Meter...".println();
    for (s in SmartOffice!EnergyMeter.all) {
        s.energyConsumed = 0.0d;
    }
}
// --- UPDATE DATA MANAGER (Write real-world data to the twin) ---
var dataManager = SmartOffice!DataManager.all.first();
if (dataManager.isDefined()) {
    // Storing the current Real Power (from CSV) in the Data Manager
    //dataManager.currentPowerUsage = simulateData.powerConsumption;
    }

// --- UPDATE DIGITAL TWIN (Set overall status) ---
var digitalTwin = SmartOffice!DigitalTwin.all.first();
if (digitalTwin.isDefined()) {
    // Set status to OPERATIONAL since the engine is running
    digitalTwin.status = SmartOffice!SystemStatus#OPERATIONAL;

    // Placeholder for Predictive Model output (Optional, for integration testing)
    var predictiveModel = digitalTwin.predictiveModel.first();
    if (predictiveModel.isDefined()) {
        // Simple prediction: Next outdoor temperature will be 0.5 degrees lower
        predictiveModel.outputVar = (simulateData.outdoorTemperature - 0.5).format("%.2f") + " °C";
    }
}
//----------------------------------------------------------------------------------------------------


// --- Calculate Total Building Capacity ---
var totalCapacity = SmartOffice!Room.all.collect(r | r.capacity).sum();
totalCapacity.println();

for (room in SmartOffice!Room.all) {

    var previousTemp = room.currentTemp;

    //Initialisation
    // If temp is 0.0, set a realistic starting point.
    //Basically this line of code is to simulate the first indoor temperature because my dataset dont have indoor temperature
    if (previousTemp == uninitializedTemp ) {
        previousTemp = outdoorTemp + INITIAL_INDOOR_OFFSET;
    }

    //Calculate new temp for next timestep based on three factors:
    //Natural heat transfer ,Temperature provided by hvac and Heat generated by occupants

    //1. Natural heat transfer (pulls temp towards outside)
    // Read the insulation value from the model for this specific room
    var roomInsulation = room.insulation;
    var delta_from_outside = (outdoorTemp - previousTemp) * roomInsulation;

    //2.Heat/Cooling from HVAC (pulls temp towards target)
    var delta_from_ac = 0.0d;

    if (room.hvac.isDefined()) {

        //Check if room is too hot
        if(previousTemp > targetTemp + comfortZone){
            room.hvac.status = true; //Turn on COOLING
            updatedSystems = updatedSystems + 1;

            //Calculate how far are we from the targetTemp
            var gap = (previousTemp - targetTemp).abs();

            var loadFactor = gap / 2.0;
            if (loadFactor > 1.0) { loadFactor = 1.0; }

            room.hvac.powerUsage = (AC_POWER_USAGE / COOLING_COP) * loadFactor;

            //Calculate cooling effect
            //Negative delta indicating lowering the temperature
            //loadFactor here means if the AC is running at 25% power, the cooling effect will also be 25%
            delta_from_ac = (targetTemp - previousTemp) * AC_POWER_COEFFICIENT * loadFactor;
        }

        //Check if room is too cold
        else if(previousTemp < targetTemp - comfortZone){
            room.hvac.status = true;  // Turn on HEATING
            updatedSystems = updatedSystems + 1;

            //Calculate how far are we from the targetTemp
            var gap = (targetTemp - previousTemp).abs();

            // If gap is huge (5 degrees), run at 100% power.
            // If gap is tiny (0.5 degrees), run at 10% power.
            // Assume 5 degrees gap, loadFactor = 5/2 = 2.5 > 1 so loadFactor = 1.0 meaning run in 100% load
            var loadFactor = gap / 2.0;
            if (loadFactor > 1.0) { loadFactor = 1.0; }

            //in real building , we dont use full 8kw of power to get 8kw of heat , we only use part of it
            room.hvac.powerUsage = (HEATER_POWER_USAGE / HEATER_COP) * loadFactor;

             //eg:(target - previous) * 0.1,  this is to simulate 10% increase of the temperature
             delta_from_ac = (targetTemp - previousTemp) * HEATER_POWER_COEFFICIENT * loadFactor;
        }

        //Check if it's in comfort zone
        else{
            room.hvac.status = false; // Turn OFF
            room.hvac.powerUsage = 0.0;
            delta_from_ac = 0.0;
        }
    }

    //3. Heat Generated by occupant logic
    var heat_from_occupants = 0.0;
    var estimatedRoomOccupancy = 0.0;

    // A checking for occupancy, because the dataset have some huge number of occupancy
    // and a huge number of occupancy is unrealistic
    if (totalCapacity > 0 and room.capacity > 0) {

        // 1. Proportional Distribution
        var roomShare = room.capacity / totalCapacity;
        estimatedRoomOccupancy = totalBuildingOccupancy * roomShare;

        // 2. Capping at Max Capacity
        //    If the estimate is higher than the room's capacity,
        //    just use the room's capacity.
        if (estimatedRoomOccupancy > room.capacity) {
            estimatedRoomOccupancy = room.capacity;
        }

        // 3. Calculate heat gain
        heat_from_occupants = estimatedRoomOccupancy * HEAT_GAIN_PER_PERSON;
    }

    //Calculate the final new temperature
    var newTemp = previousTemp + delta_from_outside + delta_from_ac + heat_from_occupants;

    //Update model with new temp
    room.currentTemp = newTemp;

    //Power Calculation
    //Get HVAC Power (if it exists and is running)
    var hvacPower = 0.0;
    if (room.hvac.isDefined() and room.hvac.status == true) {
       hvacPower = room.hvac.powerUsage; // room.hvac.powerUsage is the calculated HVAC power (e.g., 2.6 kW)
    }

    // Calculate Base Load in kw (Lights + Computers)
    //Base load means the minimum amount of energy a building uses all the time, even when it is empty
    var plugPower = 0.0;

    //If people are in the room, lights/PCs are ON.
    if (estimatedRoomOccupancy > 0) {
        plugPower = room.baseLoad;
    } else {
        //If empty, use only 10% power (standby/security lights)
        plugPower = room.baseLoad * 0.1;
    }

    //Total Room Power
    var totalRoomPower = hvacPower + plugPower;

    // Update Temperature Sensor
    var tempSensor = room.sensors.selectOne(s | s.isTypeOf(SmartOffice!TemperatureSensor));
    if (tempSensor.isDefined()) {
        // CORRECTED: Using 'temperature' attribute name
        tempSensor.temperature = newTemp;
    }

    // Update Energy Meter (The Energy Calculation remains the same)
    var energySensor = room.sensors.selectOne(s | s.isTypeOf(SmartOffice!EnergyMeter));
    if (energySensor.isDefined()) {
        // Get power usage
        var energyThisStep = totalRoomPower * TIME_STEP_HOURS;
        energySensor.energyConsumed = energySensor.energyConsumed + energyThisStep;
    }

    // --- UPDATE ABSTRACT SENSOR ATTRIBUTES (The common Timestamp) ---
    for (s in room.sensors) {
        s.timeStamp = simulateData.date.asDate("yyyy-MM-dd HH:mm:ss"); // Use the CSV date string for the timestamp
    }
}

('Simulation complete. ' + updatedSystems + ' HVAC systems running.').println();

// Save the changes back to the .smartoffice model file
SmartOffice.store();
'Model saved.'.println();